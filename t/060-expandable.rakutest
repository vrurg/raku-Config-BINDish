use v6.d;
use Test::Async;
use Config::BINDish::Expandable;
use Config::BINDish::INET;
use Config::BINDish;

plan 2;

subtest "Nesting" => {
    plan 2;
    my $cfg-src = q:to/CFG/;
        default-server "S%d";
        server "S1" {
            url https://s1.local;
        }
        server "S2" {
            url https://s2.local;
        }
        resources {
            url "{/server("{/default-server}").url}";
        }
        CFG

    for 1,2 -> $id {
        my $cfg = Config::BINDish.new(:!strict);
        $cfg.read(string => sprintf($cfg-src, $id));
        is $cfg.get(:resources => "url"),
           sprintf('https://s%d.local', $id),
           "URL is for server S$id";
    }
}

subtest "Base", {
    plan 9;
    my $cfg-src = q:to/CFG/;

        name "test config";
        description "Meaningless {name}...";
        test1 "block {server(S1, addr).name} is visible";
        test2 "path from a subblock: {server(S1, addr).resource(Img).path}";
        test3 "integer: {server(S1, addr).resource(Img).max-size}";
        test4 "subblock name with spaces: {server(S1, addr).meta('spaced name').id}";

        server "S1" addr {
            name "server addresses";
            description "Block about {name}.";
            test1 "block-related: {meta("spaced name").id}";
            base https://localhost/;
            resource "Img" {
                path "img";
                max-size 1024;
            }
            meta "spaced name" {
                id 42;
            }
            general {
                scripts "v3.14";
            }
        }

        server "S1" path {
            root "/usr/local/www";
            img-pool "{root}/{/server("S1", addr).resource(Img).path}";
            scripts "{root}/scripts/{/server(S1, addr).general.scripts}";
        }
        CFG

    my $cfg = Config::BINDish.new(:!strict);
    $cfg.read(string => $cfg-src);

    my $top = $cfg.top;

    is $top.value('description'), "Meaningless test config...", "the most basic expansion case at top-level";
    is $top.get(:server('S1', 'addr') => 'description'),
       "Block about server addresses.",
       "the most basic expansion case in a block";

    is $top.value('test1'),
       "block server addresses is visible",
       "top-level option see a later declared block";

    is $top.value('test2'),
       "path from a subblock: img",
       "top-level option see a second-level block";

    is $top.value("test3"),
       "integer: 1024",
       "can expand with a non-string value";

    is $top.value("test4"),
       "subblock name with spaces: 42",
       "subblock is seen when its name contains whitespaces";

    my $blk1 = $top.block("server", name => "S1", class => 'addr');

    is $blk1.value("test1"), "block-related: 42", "relative expansion within a block";

    my $blk2 = $top.block("server", :name<S1>, :class<path>);

    is $blk2.value("img-pool"), "/usr/local/www/img", "expanded from both local and another block options";
    is $blk2.value("scripts"), "/usr/local/www/scripts/v3.14", "reference to a nameless block";
}

done-testing;